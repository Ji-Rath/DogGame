// 2020-07-22 13:11:54
#event properties (no comments/etc. here are saved)
parent_index = -1;
uses_physics = false;

#event create

GameType = -1;
timer[0] = 1*60; //Wait 1 second before spawning characters
timer[1] = -1 //MiniGame time

//Instances to destroy after the minigame
Instances = ds_list_create();

//Open Effect
OpenEffect = 0;
OpenFraction = 0;
Destroy = false;
ShowEnemy = false;
EndTurn = true;

enum Game
{
    CrabAttack,
    Crab1,
    GlovesOff,
    Hamburger,
    Chips,
    PanAttack,
    BroomAttack,
    CrabWeigh,
    Highfive
}
alarm[0] = 1;

DrawSmall = false;
SmallScreenPosX = 0;
SmallScreenPosY = 0;

#event alarm0

//Switch to the specified minigame
switch(GameType)
{
    case Game.CrabAttack:
        ds_list_add(Instances, instance_create_layer(room_width/4,room_height/2,"Instances",oPlayerBoob));
        ds_list_add(Instances, instance_create_layer(room_width/1.5,room_height/2,"Instances",oEnemyCrabArm));
    break;
    
    case Game.Crab1:
        ds_list_add(Instances, instance_create_layer(room_width/2,room_height/2+50,"Instances",oCrab1Arm));
        ds_list_add(Instances, instance_create_layer(room_width/2,room_height/2-50,"Instances",oCrab1Player));
    break;
    
    case Game.GlovesOff:
        ds_list_add(Instances, instance_create_layer(room_width/2,room_height/2,"Instances",oPlayerGloveSmack));
    break;
    
    case Game.Chips: 
        ds_list_add(Instances, instance_create_layer(room_width/2,room_height/2+200,"Instances",oChipBag));
    break;
    
    case Game.PanAttack:
        ds_list_add(Instances, instance_create_layer(room_width/2+300,room_height/2,"Instances",oPan));
    break;
    
    case Game.BroomAttack:
        ds_list_add(Instances, instance_create_layer(room_width/2,room_height/2,"Instances",oBroom));
    break;
    
    case Game.CrabWeigh:
        ds_list_add(Instances, instance_create_layer(room_width/2+200+random(200),room_height/2+random_range(-150,150),"Instances",oCrabWeigh));
        ds_list_add(Instances, instance_create_layer(room_width/2-200+random(200),room_height/2+random_range(-150,150),"Instances",oCrabWeigh));
        ds_list_add(Instances, instance_create_layer(room_width/2,room_height/2+100,"Instances",oCrabBalance));
    break;
    
    case Game.Highfive:
        DrawSmall = true;
        SmallScreenPosX = 250+100;
        SmallScreenPosY = 125+100;
        ds_list_add(Instances, instance_create_layer(SmallScreenPosX,SmallScreenPosY,"Instances",oHighfive));
    break;
}


#event step

//Minigame intro transition and outro effect
if(!Destroy)
{
    
    if(OpenEffect != 1)
    {
        OpenFraction += (2*pi)/120;
    }
}
else
{
    if(OpenEffect != 0)
    {
        OpenFraction -= (2*pi)/60;
    }
    else
    {
        //Destroy self and instances created for the minigame
        instance_destroy();
        for(var i=0;i<ds_list_size(Instances);i++)
        {
            var Instance = ds_list_find_value(Instances,i);
            if(instance_exists(Instance))
            {
                instance_destroy(Instance);
            }
        }
    }
}

OpenEffect = sin(OpenFraction);

//After set time, execute game
if(timer[0] > 0)
{
    timer[0] -= 1;
}
else if (timer[0] != -1)
{
    timer[0] = -1;
    timer[1] = 4*60;
}

//End of minigame
if(timer[1] > 0)
{
    timer[1] -= 1;
}
else if (timer[1] != -1)
{
    if(DrawSmall)
    {
        Destroy = true;
        oBattleManager.DrawGUI = true;
    }
    else
    {
        //Prevent decimals
        with(oBattleManager)
        {
            global.PlayerHP = ceil(global.PlayerHP);
            global.PlayerPP = ceil(global.PlayerPP);
            EnemyBattle.Health = ceil(EnemyBattle.Health);
        }
        Destroy = true;
        if(EndTurn)
        {
            oBattleManager.BattleStageEnd = true;
        }
        else
        {
            oBattleManager.UpdateStats = true;
            oBattleManager.visible = true;
            oBattleMenuParent.visible = true;
            oBattleManager.DrawGUI = true;
        }
    }
    
    timer[1] = -1;
}

#event draw

if(alarm[0] == -1)
{
    //Draw MiniGame Boundries
    draw_set_color(c_ltgray);
    
    if(DrawSmall)
    {
        draw_roundrect(SmallScreenPosX+250,SmallScreenPosY+(125*OpenEffect),SmallScreenPosX-250,SmallScreenPosY-(125*OpenEffect),false)
    }
    else
    {
        draw_roundrect(room_width/2+500,room_height/2+(250*OpenEffect),room_width/2-500,room_height/2-(250*OpenEffect),false)
        if(timer[1] != -1)
        {
            draw_set_color(c_red);
            var Time = round((timer[1]/(4*60))*12)/12;
            draw_rectangle(room_width/2-500+(Time*1000),room_height/2+250,room_width/2-500,room_height/2+225,false);
        }
    }
    
    draw_set_color(c_white);
    
    for(i=0;i<ds_list_size(Instances);i++)
    {
        with(ds_list_find_value(Instances,i))
        {
            image_alpha = oMiniGame.OpenEffect;
        }
    }
}