// 2020-06-18 01:38:15
#event properties (no comments/etc. here are saved)
parent_index = -1;
uses_physics = false;

#event create

TextIntro = [""];
TextDuring = [""];
Health = 0;
AttackDamage = 0;
Name = "";

EnemyAttacks = [];

HealthChanged = false;

timer[0] = -1;
Angle = 0;
IsDead = false;

Vulnerable = false;

#event step

//Delay death animation
if(Health <= 0 && timer[0] == -1 && oBattleManager.BattleStage >= 6 && !IsDead && path_index == -1)
{
    timer[0] = 1*60;
}

//Perform enemy death animation
if(timer[0] > 0)
{
    timer[0] -= 1;
}
else if(timer[0] != -1)
{
    path_start(pathBattleEnemyDeath, 20, path_action_stop, false);
    timer[0] = -1;
}

//Show victory message
if(path_position == 1 && !IsDead)
{
    scrCreateBattleBox(["You Win!"]);
    IsDead = true;
}

//Spin enemy while death animation is running
if(path_index != -1)
{
    Angle += 20;
}

//If enemy is vulnerable, rotate enemy
if(Vulnerable)
{
    if(Angle <= 180 && Health > 0)
        Angle += 5;
}
else
{
    if(Angle != 0 && Health > 0)
        Angle -= 15;
}

#event draw

//Shake enemy if damaged
var XPos = x + choose(oBattleManager.Shake[1],-oBattleManager.Shake[1]);
var YPos = y;
var Alpha = 1;
var Scale = 1;
var YDivision = 1;

//Draw enemy if the minigame has the option enabled
if(instance_exists(oMiniGame))
{
    if(oMiniGame.ShowEnemy)
    {
        Scale = 1-(oMiniGame.OpenEffect/1.75);
    }
    else
    {
        YPos = y + oMiniGame.OpenEffect*250;
        Alpha = 1-(oMiniGame.OpenEffect/2);
    }
    
    if(instance_exists(oPan))
    {
        if(oPan.Hit)
        {
            YDivision = 2;
        }
    }
}

//Draw enemy sprite at specified dimensions
draw_sprite_ext(sprite_index,image_index,XPos,YPos,Scale,Scale/YDivision,Angle,c_white,Alpha);

//Update enemy health on screen if it has changed
var HC = HealthChanged;
with(oBattleManager)
{
    if (EnemyBattle != noone && DrawEnemyHealth != EnemyBattle.Health)
    {
        HC = true;
    }
    if(!UpdateStats)
    {
        HC = false;
    }
}
HealthChanged = HC;

//Draw enemy health if it has changed
if((oBattleManager.visible) && HealthChanged)
    draw_healthbar(XPos-100,YPos-50,XPos+100,YPos-75,oBattleManager.DrawEnemyHealth*10,c_gray,c_red,c_red,0,true,false);
    
